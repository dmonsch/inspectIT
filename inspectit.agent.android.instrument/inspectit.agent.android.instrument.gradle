defaultTasks 'releaseAndAnalyze'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
}

sourceSets { 
   main { 
      resources { 
         srcDir new File('src/main/resources')
      } 
   }   
}

/** Setting compile configuration as plugin in Eclipse needs it */
configurations {
	compile {
		extendsFrom configurations.androidInstrumentProd
	}
	testCompile {
		extendsFrom configurations.androidInstrumentTest
		extendsFrom configurations.jmhbase
	}
}

ext {
	distJarName = 'inspectit-android-instrument'
	releaseName = "inspectit-android-instrument-${versionInfo}.zip"
}

dependencies {
	compile project(":inspectit.agent.android")
	compile project(':inspectit.shared.all')
	
    testCompile 'junit:junit:4.12'
}

jar {
	archivesBaseName = distJarName
	libsDirName = buildReleaseRoot
	
	manifest {
		attributes(
			'Main-Class': 'rocks.inspectit.android.instrument.cli.InstrumentationMain'
		)
	}
}

shadowJar {
	/** Apply the mock, this is necessary because otherwise we get a runtime exception that the class "BroadcastReceiver" could not be found */
	relocate 'rocks.inspectit.android.instrument.mock', 'android.content'
}

task packageGradlePluginDependencies(type: Zip) {
	dependsOn "shadowJar"
	dependsOn ":inspectit.agent.android:shadowJar"
	
	from ("build/release/") {
		include "inspectit-android-instrument-all.jar"
	}
	from ("lib") {
		include "*"
		into "lib"
	}
	from ("../inspectit.agent.android/build/release") {
		include "inspectit.agent.android-all.jar"
	}
	
	archiveName 'inspectIT-Android.zip'
    destinationDir = file('build/release')
}

task releaseAndAnalyze {
	description = "Runs all unit tests, all checks and releases the jar."
	group = 'Release'
	dependsOn(analyze, jar)
}