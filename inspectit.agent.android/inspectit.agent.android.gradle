defaultTasks 'releaseAndAnalyze'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    jcenter()
}

/** Setting compile configuration as plugin in Eclipse needs it */
configurations {
	compile {
		extendsFrom configurations.agentJavaProd
	}
	testCompile {
		extendsFrom configurations.agentJavaTest
		extendsFrom configurations.jmhbase
	}
}

dependencies {
    /** Agent dependencies. */
	compile group: 'com.google.android', name: 'android', version: '4.1.1.4'
	
	compile project(":inspectit.shared.android")
	compile project(":inspectit.agent.java.sdk")
	
	compile 'io.opentracing:opentracing-api:0.20.9'
	compile 'io.opentracing:opentracing-noop:0.20.9'
	
    compile 'junit:junit:4.12'
    
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.powermock', name: 'powermock-core', version: '1.7.3'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.7.3'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.3'
}

shadowJar {
	relocate 'com.fasterxml', 'rocks.org.codehaus'
	
	exclude 'META-INF/*'
	
	/** Only this dependencies are included in the build */
	dependencies {
		include(project(":inspectit.shared.android"))
		
		include(project(":inspectit.agent.java.sdk"))
		include(dependency('io.opentracing:opentracing-api:0.20.9'))
		include(dependency('io.opentracing:opentracing-noop:0.20.9'))
		
		include(dependency('com.fasterxml.jackson.core:jackson-annotations:2.7.0'))
		include(dependency('com.fasterxml.jackson.core:jackson-core:2.7.2'))
		include(dependency('com.fasterxml.jackson.core:jackson-databind:2.7.2'))
	}
}

task releaseAndAnalyze {
	description = "Runs all unit tests, all checks and releases the jar."
	group = 'Release'
	dependsOn(analyze, jar)
}